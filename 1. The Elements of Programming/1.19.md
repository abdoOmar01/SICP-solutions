Let $T_{pq}$ be the transformation of the pair $(a, b)$ according to:

$$ a\gets bq+aq+ap$$
$$ b\gets bp+aq$$

By applying $T$ a second time we get:

$$a\gets (bp_1+aq_1)q_2+(bq_1+aq_1+ap_1)q_2+(bq_1+aq_1+ap_1)p_2$$
$$b\gets (bp_1+aq_1)p_2 + (bq_1+aq_1+ap_1)q_2$$

By simplifying both equations we get:

$$a\gets b(q_1p_2+q_1q_2+p_1q_2)+a(q_1p_2+q_1q_2+p_1q_2)+a(p_1p_2+q_1q_2)$$

$$b\gets b(p_1p_2+q_1q_2)+a(q_1p_2+q_1q_2+p_1q_2)$$

From the problem definition, we know that applying the transformation $T_{pq}$ twice is the same as applying the transformation $T_{p′q′}$, by observing the two equations we find that:

$$ p′ = p_1p_2+q_1q_2 $$

$$ q′= q_1p_2+q_1q_2+p_1q_2 $$

From the special case of $p_1=p_2$ and $q_1=q_2$:

$$ p′= p^2 + q^2 $$

$$ q′= 2pq + q^2 $$

---

```scm
(define (fib n)
 (fib-iter 1 0 0 1 n))
(define (fib-iter a b p q count)
 (cond ((= count 0) b)
       ((even? count)
        (fib-iter a
                  b
                  (+ (* p p) (* q q))   ; compute p′
                  (+ (* 2 p q) (* q q)) ; compute q′
                  (/ count 2)))
        (else (fib-iter (+ (* b q) (* a q) (* a p))
                        (+ (* b p) (* a q))
                        p
                        q
                        (- count 1)))))
```